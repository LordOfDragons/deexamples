pin Dragengine.ConversationSystem
pin Dragengine.Gui.Events
pin Dragengine.Scenery
pin Dragengine.Utils



/**
 * \brief Game Application Class.
 * 
 * BaseGameApp provides basic starting behavior.
 */
class GameApp extends BaseGameApp
	public var WindowOverColor windowOverBlack
	
	
	/**
	 * \brief Create Game Application.
	 * 
	 * Set here properties used by BaseGameApp during initGame(). All other initialization
	 * work should be placed inside initGame() or one of the hook methods called by
	 * BaseGameApp.initGame().
	 */
	public func new()
		// Path to the GUI Theme file to use. If not set uses BaseGameApp.defaultPathGuiTheme
		pathDesktopGuiTheme = "/ui/ui.guitheme.xml"
		
		// To use translation support create one or more language packs and uncomment the
		// properties below with the appropriate values.
		// 
		//pathLanguagePacks = "/content/langpacks"
		//defaultLanguagePackName = UnicodeString.newFromUTF8( "English" )
	end
	
	/**
	 * \brief Initialize game.
	 * 
	 * Create required objects here and start loading. Lengthy loading work should not be
	 * waited for in this method. Methods like startLoadGameWorld() will notify you when
	 * the lengthy loading work is done.
	 * 
	 * See BaseGameApp.initGame() documentation for the supported hook methods.
	 */
	public func void initGame()
		// super-call has to come first
		super.initGame()
		
		// Start game by showing the Drag[en]gine Game Engine logo and optionally other logos
		// you would like to show. WindowLogo.show*() does all the work of showing a logo or
		// video with optional sound. Once the window is finished (the user can skip it)
		// the provided action listener is called. This can be creating a class implementing
		// ActionListener interface or by using BlockActionListener. This is a convenience
		// class allowing to use a block as an action and is suitable for quick code insertion
		// like this. Using a class implementing ActionListener is though favorable since it
		// makes it easier to chain actions in an understandable way.
		// 
		// Showing the Drag[en]gine Game Engine logo is not required at all to use this game
		// engine but we highly appreciate it if you do so.
		
		WindowLogo.showDragengineLogo( this.getDesktop(), BlockActionListener.new( \
			block ActionEvent event
				launchApplication()
			end ) )
	end
	
	/**
	 * \brief Launch example application.
	 * 
	 * Called after logo windows are finished.
	 */
	public func void launchApplication()
		// BaseGameApp adds a WindowConsole which can be toggled by pressing F1 key. Adding
		// a ShoutConsole shows the last messages without needing to show the console.
		getWindowGameWorld().addWidget(ShoutConsole.new(getConsole()))
		
		// window for blending over black. used for one of the examples
		windowOverBlack = WindowOverColor.new()
		windowOverBlack.setDesignerSelector("Window.OverBlack")
		windowOverBlack.setTimeFadeIn(0.5)
		windowOverBlack.setTimeStay(0.5)
		windowOverBlack.setTimeFadeOut(0.5)
		getDesktop().addWindow(windowOverBlack)
		
		// Logging can be done by adding messages to the console
		getConsole().addMessage( "Loading world..." )
		
		// startLoadGameWorld is a simple way to start loading a game world into the
		// WindowGameWorld created by BaseGameApp. StartGame is a script class created
		// alongside this application class. It will be notified when the world loading
		// finishes and contains the required code to finish starting the game.
		// 
		// To use this create a world file and change the filename
		startLoadGameWorld(StartGame.new(getWindowGameWorld(), "/world/world1.deworld"))
	end
	
	/**
	 * \brief Cleaning up.
	 * 
	 * Will be called before the game engine shuts down the game. This method will be also
	 * called if the initGame() threw an exception before finishing. Make sure to check if
	 * objects have been initialized before cleaning them up.
	 */
	public func void cleanUp()
		// your cleaning up code has to come before the super-call
		
		// super-call has to come last
		super.cleanUp()
	end
	
	/** \brief Create element classes and add them to element class list. */
	protected func void createAndAddElementClasses()
		super.createAndAddElementClasses()
		
		var ElementClassList eclist = getElementClassList()
		eclist.add(CeilingLampClass.new())
		eclist.add(TouchStandClass.new())
		eclist.add(PlayerActorClass.new())
		eclist.add(DragonessClass.new())
		eclist.add(TriggerPlayerRunsClass.new())
		
		// add XML element classes
		getLoaders().getElementClass().addXmlElementClasses(eclist, "/model", true)

	end
	
	/** \brief Create conversation playback. */
	public func BaseConversationPlayback createConversationPlayback(Conversation conversation)
		return ATConversationPlayback.new(conversation)
	end
	
	/** \brief Game app instance. */
	static public func GameApp getGameApp()
		return BaseGameApp.getApp() cast GameApp
	end
end
