pin Dragengine.Scenery


/**
 * Can class.
 */
class CanClass extends PhysicsElementClass
	public var ECBehaviorGrabSpot grabSpot
	public var ECBehaviorAttachable attachable
	public var ECBehaviorVRHandPointedAt vrHandPointedAt
// 	public var ECBHighlightPointedAt highlightPointedAt
	
	
	
	/** Create element class. */
	public func new() this("Can")
	end
	
	/** Create element class. */
	public func new(String name) super(name)
		// Set model, skin and rig to use
		getComponent().setCommonPath("/content/models/can", "can.demodel", "can.deskin", "can.derig")
		
		// Set mass of can to 50g
		getCollider().getCollider().getWeight().setValue(0.05)
		
		// Make can grabbable
		grabSpot = ECBehaviorGrabSpot.new(this, getCollider())
		
		var ShapeList shape = ShapeList.new()
		shape.addSphere(Vector.new(), 0.05) // Can has radius of 0.0375. Make it a bit bigger
		grabSpot.getTouchCollider().getShape().setShapeList(shape)
		
		// Make can attachable
		attachable = ECBehaviorAttachable.new(this, getCollider())
		
		// Player can point at object
		vrHandPointedAt = ECBehaviorVRHandPointedAt.new(this)
		
		// Highlight object if player points at it
// 		highlightPointedAt = ECBHighlightPointedAt.new(this, getComponent(), getCollider(), vrHandPointedAt)
	end
	
	/** Create copy of element class for use with XML Subclassing. */
	public func ElementClass duplicateNamed(String classname)
		return CanClass.new(classname)
	end
end
