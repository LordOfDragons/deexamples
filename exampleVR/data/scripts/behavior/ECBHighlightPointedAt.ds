pin Dragengine.Scenery


/**
 * Behavior adding support to highlight element if pointed at by the user.
 * Uses ECBehaviorOutline to do the highlighting in response to
 * ECBehaviorVRHandPointedAt events.
 */
class ECBHighlightPointedAt extends DefaultECBehavior
	/** Behavior instance. */
	class Instance extends DefaultECBehaviorInstance
		/** Pointed at changed. */
		class PointedAtChanged extends ECBehaviorVRHandPointedAt.DefaultListener
			protected var Instance pInstance
			
			public func new(Instance instance)
				pInstance = instance
			end
			
			public func void handStartsPointingAt(ECBehaviorVRHandPointedAt.Instance instance,\
			ECBehaviorVRHandPointAt.Instance hand)
				pInstance.show()
			end
			
			public func void handStopsPointingAt(ECBehaviorVRHandPointedAt.Instance instance,\
			ECBehaviorVRHandPointAt.Instance hand)
				pInstance.hide()
			end
		end
		
		
		protected var ECBHighlightPointedAt pECBehavior
		protected var ECBehaviorVRHandPointedAt.Instance pVRHandPointedAt
		protected var ECBehaviorOutline.Instance pOutline
		
		
		
		/** Create behavior instance. */
		public func new(ECBHighlightPointedAt ecbehavior, BehaviorElement element) super(element)
			pECBehavior = ecbehavior
			
			pVRHandPointedAt = ecbehavior.getVRHandPointedAt().instance(element)
			pOutline = ecbehavior.getOutline().instance(element)
			
			pVRHandPointedAt.addListener(PointedAtChanged.new(this))
		end
		
		
		
		/** Show highlight. */
		public func void show()
			pOutline.setVisible(true)
		end
		
		/** Hide highlight. */
		public func void hide()
			pOutline.setVisible(false)
		end
	end
	
	
	
	protected var ECBehaviorVRHandPointedAt pVRHandPointedAt
	protected var ECBehaviorOutline pOutline
	
	
	
	/** Create behavior and add it to element class. */
	public func new(BehaviorElementClass eclass, ECBehaviorComponent component,\
	ECBehaviorCollider collider, ECBehaviorVRHandPointedAt vrHandPointedAt) super(eclass)
		if vrHandPointedAt == null
			throw ENullPointer.new("vrHandPointedAt")
		end
		
		pVRHandPointedAt = vrHandPointedAt
		
		pOutline = ECBehaviorOutline.new(eclass, component, collider, "highlightPointedAt")
		pOutline.getSkin().setPath("/content/materials/outline/outlinePointAt.deskin")
		
		eclass.addBehavior(this)
	end
	
	/** Dispose of behavior. */
	public func void dispose()
		pOutline = null
		pVRHandPointedAt = null
		super.dispose()
	end
	
	
	
	/** VR hand pointed at behavior. */
	public func ECBehaviorVRHandPointedAt getVRHandPointedAt()
		return pVRHandPointedAt
	end
	
	/** Outline behavior. */
	public func ECBehaviorOutline getOutline()
		return pOutline
	end
	
	
	
	/** Get instance in element from owner element class. */
	public func Instance instance( BehaviorElement element )
		return element.getInstanceAt(getInstanceIndex()) cast Instance
	end
	
	
	
	/** Create behavior instance. */
	public func ECBehaviorInstance createInstance(BehaviorElement element)
		return Instance.new(this, element)
	end
end
