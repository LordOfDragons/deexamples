/* 
 * Drag[en]gine Graphics Test
 *
 * Copyright (C) 2018, Pl√ºss Roland ( roland@rptd.ch )
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */


pin Dragengine.Commands
pin Dragengine.Gui
pin Dragengine.Utils


/**
 * \brief Input processor for UI window.
 * 
 * If binding matches behaves like BindingInputProcessor accepting only button input
 * and ignoring mouse device input. If no binding matches behaves like WidgetInputProcessor
 * connected to Desktop.
 */
class UIInputProcessor extends DefaultInputProcessor
	private var WidgetInputProcessor pDesktopProcessor
	
	
	
	/**
	 * \brief Create input processor.
	 */
	public func new( Desktop desktop )
		pDesktopProcessor = WidgetInputProcessor.new( desktop )
	end
	
	
	
	/** \brief Process input event. */
	public func void inputEvent( InputEvent event )
		var GraphicsTestApp app = GraphicsTestApp.getGTApp()
		
		if app.getWindowUI().getBlockingWindow() != null
			// a modal window is shown. do not interfere
			pDesktopProcessor.inputEvent( event )
			return
		end
		
		select event.getType()
		case InputEventType.keyPress, InputEventType.buttonPress
			var Binding binding = app.getBindingManager().findButtonBinding( \
				event.getDevice(), event.getCode() )
			if binding != null
				binding.getCommand().start()
				return
			end
			
		case InputEventType.keyRelease, InputEventType.buttonRelease
			var Binding binding = app.getBindingManager().findButtonBinding( \
				event.getDevice(), event.getCode() )
			if binding != null
				binding.getCommand().stop()
				return
			end
		end
		
		pDesktopProcessor.inputEvent( event )
	end
end
